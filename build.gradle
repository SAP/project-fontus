plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'java'
    id 'maven-publish'
    id 'idea'
}
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar


version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

tasks.jar.configure {
    classifier = 'default'
}

dependencies {
    compile files('libs/gsp.jar')
    compile group: 'org.apache.calcite', name: 'calcite-core', version: '1.15.0'
    compile 'org.ow2.asm:asm:9.0'
    compile 'org.ow2.asm:asm-commons:9.0'
    compile 'org.ow2.asm:asm-analysis:9.0'
    compile 'org.ow2.asm:asm-util:9.0'
    compile 'info.picocli:picocli:4.1.4'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.9'
    compile group: 'org.json', name: 'json', version: '20200518'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    compile group: 'org.mutabilitydetector', name: 'asm-nonclassloadingextensions', version: '1.0-rc2'
    testCompile('org.junit.jupiter:junit-jupiter:5.4.2')
    testCompile('org.junit.jupiter:junit-jupiter-migrationsupport:5.4.2')
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    compile 'junit:junit:4.12'
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests"
            println "\t${result.successfulTestCount} succeeded\n" +
                    "\t${result.failedTestCount} failed\n" +
                    "\t${result.skippedTestCount} skipped"
        }
    }
}

task utilJar(type: ShadowJar) {
    from(sourceSets.main.output) {
        include "**/asm_test/taintaware/**"
        include "**/asm_test/instrumentation/strategies/*"
        include "**/asm_test/utils/**"
        include "**/asm_test/Constants*"
        include "**/asm_test/config/TaintStringConfig*"
        include "**/asm_test/config/TaintMethod*"
        include "**/asm_test/config/**"
        include "**/asm_test/asm/**"
        include "**/asm_test/asm/FunctionCall*" // Because of Configuration class
        include "**/asm_test/agent/AgentConfig*" // Because of Configuration class
    }
    utilJar.baseName = "util"
}

task generatorJar(type: ShadowJar) {
    manifest {
        attributes(
                'Main-Class': 'de.tubs.cs.ias.asm_test.generator.Generator',
        )
    }
    from(project.convention.getPlugin(JavaPluginConvention).sourceSets.main.output)
    configurations = [project.configurations.runtime]

    baseName = 'generator'
}

task converterJar(type: ShadowJar) {
    manifest {
        attributes(
                'Main-Class': 'de.tubs.cs.ias.asm_test.converter.Converter',
        )
    }
    from(project.convention.getPlugin(JavaPluginConvention).sourceSets.main.output)
    configurations = [project.configurations.runtime]

    baseName = 'converter'
}

shadowJar {
    mergeServiceFiles()
    doFirst {
        manifest {
                attributes(
                        // 'Class-Path': configurations.compile.collect{it.toURI().toString()}.join(' '),
                        'Main-Class': 'de.tubs.cs.ias.asm_test.Main',
                        'Agent-Class': 'de.tubs.cs.ias.asm_test.agent.TaintAgent',
                        'Can-Redefine-Classes': true,
                        'Can-Retransform-Classes': true,
                        'Premain-Class': 'de.tubs.cs.ias.asm_test.agent.TaintAgent'
                )
        }
    }

    // This makes sure that the generated jar has no "-all" suffix
    classifier = ''

    relocate 'com.ctc', 'de.tubs.cs.ias.asm_test.com.ctc'
    relocate 'com.fasterxml', 'de.tubs.cs.ias.asm_test.com.fasterxml'
    relocate 'org.codehaus', 'de.tubs.cs.ias.asm_test.org.codehaus'
    relocate 'org.json', 'de.tubs.cs.ias.asm_test.org.json'
    relocate 'org.mutabilitydetector', 'de.tubs.cs.ias.asm_test.org.mutabilitydetector'
    relocate 'org.objectweb', 'de.tubs.cs.ias.asm_test.org.objectweb'
    relocate 'javax.activation', 'de.tubs.cs.ias.asm_test.javax.activation'
    relocate 'javax.xml.bind', 'de.tubs.cs.ias.asm_test.javax.xml.bind'
    relocate 'picocli', 'de.tubs.cs.ias.asm_test.picocli'
    relocate 'gudusoft', 'de.tubs.cs.ias.asm_test.gudusoft'
    dependsOn utilJar
    dependsOn converterJar
    dependsOn generatorJar
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from subprojects.collect { it.sourceSets.main.allSource }
}

shadowJar.finalizedBy sourcesJar

publishing {
    publications {
        shadow(MavenPublication) {
            from project.components.java
            groupId = 'de.tubs.cs.ias'
            artifactId = 'asm_test'
            version = '0.0.1-SNAPSHOT'
            artifact tasks.sourcesJar
            artifact tasks.shadowJar
        }

        util(MavenPublication) {
            groupId 'de.tubs.cs.ias'
            artifactId 'util'
            version = '0.0.1-SNAPSHOT'
            artifact utilJar
        }
    }
}
