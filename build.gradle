import groovy.text.GStringTemplateEngine
plugins {
    id 'java'
    id 'maven-publish'
    id 'idea'
}

version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    jmh
}

dependencies {
    compile 'org.ow2.asm:asm:7.2'
    compile 'org.ow2.asm:asm-analysis:7.2'
    compile 'org.ow2.asm:asm-util:7.2'
    compile 'info.picocli:picocli:4.1.4'
    compile 'org.slf4j:slf4j-api:1.7.25'
    // compile 'org.slf4j:slf4j-jdk14:1.7.25'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.9'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile group: 'org.mutabilitydetector', name: 'asm-nonclassloadingextensions', version: '1.0-rc2'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.8'
    testCompile('org.junit.jupiter:junit-jupiter:5.4.2')
    testCompile('org.junit.jupiter:junit-jupiter-migrationsupport:5.4.2')
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    jmhCompile project
    jmhCompile 'org.openjdk.jmh:jmh-core:1.23'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.23'
}

task jmh(type: JavaExec, description: 'Executing JMH benchmarks') {
    classpath = sourceSets.jmh.runtimeClasspath
    main = 'de.tubs.cs.ias.asm_test.Main'
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests"
            println "\t${result.successfulTestCount} succeeded\n" +
                    "\t${result.failedTestCount} failed\n" +
                    "\t${result.skippedTestCount} skipped"
        }
    }
}

task utilJar(type: Jar) {
    from(sourceSets.main.output) {
        include "**/asm_test/taintaware/**"
        include "**/asm_test/strategies/*"
        include "**/asm_test/Utils*"
        include "**/asm_test/Constants*"
        include "**/asm_test/config/TaintMethodConfig*"
        include "**/asm_test/config/TaintStringConfig*"
        include "**/asm_test/config/TaintMethod*"
    }
    utilJar.baseName = "util"
}

jar {
    doFirst {
        manifest {
            if (!configurations.compile.isEmpty()) {
                attributes(
                        // 'Class-Path': configurations.compile.collect{it.toURI().toString()}.join(' '),
                        'Main-Class': 'de.tubs.cs.ias.asm_test.Main',
                        'Agent-Class': 'de.tubs.cs.ias.asm_test.agent.TaintAgent',
                        'Can-Redefine-Classes': true,
                        'Can-Retransform-Classes': true,
                        'Premain-Class': 'de.tubs.cs.ias.asm_test.agent.TaintAgent'
                )
            }
        }
    }
    from {
        configurations.compileClasspath.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
    }
    dependsOn utilJar
}


publishing {
    publications {
        main(MavenPublication) {
            groupId = 'de.tubs.cs.ias'
            artifactId = 'asm_test'
            version = '0.0.1-SNAPSHOT'

            from components.java
        }

        util(MavenPublication) {
            groupId 'de.tubs.cs.ias'
            artifactId 'util'
            version = '0.0.1-SNAPSHOT'
            artifact utilJar
        }
    }
}
